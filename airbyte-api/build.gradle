import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "io.airbyte.gradle.jvm.lib"
    id "io.airbyte.gradle.publish"
    id "org.openapi.generator"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.kapt"
}

def specFile = "$projectDir/src/main/openapi/config.yaml"
def airbyteApiSpecFile = "$projectDir/src/main/openapi/api.yaml"
def airbyteApiSpecTemplateDirApi = "$projectDir/src/main/resources/templates/jaxrs-spec-api"
def workloadSpecFile = "$projectDir/src/main/openapi/workload-openapi.yaml"

def genApiServer = tasks.register("generateApiServer", GenerateTask) {
    def serverOutputDir = "$buildDir/generated/api/server"

    inputs.file specFile
    outputs.dir serverOutputDir

    generatorName = "jaxrs-spec"
    inputSpec = specFile
    outputDir = serverOutputDir

    apiPackage = "io.airbyte.api.generated"
    invokerPackage = "io.airbyte.api.invoker.generated"
    modelPackage = "io.airbyte.api.model.generated"

    schemaMappings = [
            'OAuthConfiguration'                : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceDefinitionSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'               : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationDefinitionSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'StreamJsonSchema'                  : 'com.fasterxml.jackson.databind.JsonNode',
            'StateBlob'                         : 'com.fasterxml.jackson.databind.JsonNode',
            'FieldSchema'                       : 'com.fasterxml.jackson.databind.JsonNode',
            'DeclarativeManifest'               : 'com.fasterxml.jackson.databind.JsonNode',
            'SecretPersistenceConfigurationJson': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            dateLibrary                   : "java8",
            generatePom                   : "false",
            interfaceOnly                 : "true",
            /*
            JAX-RS generator does not respect nullable properties defined in the OpenApi Spec.
            It means that if a field is not nullable but not set it is still returning a null value for this field in the serialized json.
            The below Jackson annotation is made to only keep non null values in serialized json.
            We are not yet using nullable=true properties in our OpenApi so this is a valid workaround at the moment to circumvent the default JAX-RS behavior described above.
            Feel free to read the conversation on https://github.com/airbytehq/airbyte/pull/13370 for more details.
            */
            additionalModelTypeAnnotations: "\n@com.fasterxml.jackson.annotation.JsonInclude(com.fasterxml.jackson.annotation.JsonInclude.Include.NON_NULL)",

            // Generate separate classes for each endpoint "domain"
            useTags                       : "true"
    ]
}

def genApiClient = tasks.register("generateApiClient", GenerateTask) {
    def clientOutputDir = "$buildDir/generated/api/client"

    inputs.file specFile
    outputs.dir clientOutputDir

    generatorName = "java"
    inputSpec = specFile
    outputDir = clientOutputDir

    apiPackage = "io.airbyte.api.client.generated"
    invokerPackage = "io.airbyte.api.client.invoker.generated"
    modelPackage = "io.airbyte.api.client.model.generated"

    schemaMappings = [
            'OAuthConfiguration'                : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceDefinitionSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'               : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationDefinitionSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'StreamJsonSchema'                  : 'com.fasterxml.jackson.databind.JsonNode',
            'StateBlob'                         : 'com.fasterxml.jackson.databind.JsonNode',
            'FieldSchema'                       : 'com.fasterxml.jackson.databind.JsonNode',
            'SecretPersistenceConfigurationJson': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    library = "native"

    generateApiDocumentation = false

    configOptions = [
            dateLibrary  : "java8",
            generatePom  : "false",
            interfaceOnly: "true"
    ]
}

def genApiClient2 = tasks.register("genApiClient2", GenerateTask) {
    def clientOutputDir = "$buildDir/generated/api/client2"

    inputs.file specFile
    outputs.dir clientOutputDir

    generatorName = "kotlin"
    inputSpec = specFile
    outputDir = clientOutputDir

    apiPackage = "io.airbyte.api.client2.generated"
    invokerPackage = "io.airbyte.api.client2.invoker.generated"
    modelPackage = "io.airbyte.api.client2.model.generated"

    schemaMappings = [
            'OAuthConfiguration'                : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceDefinitionSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'               : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationDefinitionSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'StreamJsonSchema'                  : 'com.fasterxml.jackson.databind.JsonNode',
            'StateBlob'                         : 'com.fasterxml.jackson.databind.JsonNode',
            'FieldSchema'                       : 'com.fasterxml.jackson.databind.JsonNode',
            'SecretPersistenceConfigurationJson': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            generatePom         : "false",
            interfaceOnly       : "true"
    ]

    doLast {
        /*
         * UPDATE ApiClient.kt to use Failsafe.
         */
        def apiClientFile = file('build/generated/api/client2/src/main/kotlin/org/openapitools/client/infrastructure/ApiClient.kt')
        def apiClientFileText = apiClientFile.text

        // replace class declaration
        apiClientFileText = apiClientFileText.replace(
                'open class ApiClient(val baseUrl: String, val client: OkHttpClient = defaultClient) {',
                'open class ApiClient(val baseUrl: String, val client: OkHttpClient = defaultClient, val policy : RetryPolicy<Response> = RetryPolicy.ofDefaults()) {')

        // replace execute call
        apiClientFileText = apiClientFileText.replace(
                'val response = client.newCall(request).execute()',
                '''val call = client.newCall(request)
        val failsafeCall = FailsafeCall.with(policy).compose(call)
        val response: Response = failsafeCall.execute()''')

        // add imports if not exist
        if (!apiClientFileText.contains("import dev.failsafe.RetryPolicy")) {
            def newImports = '''import dev.failsafe.RetryPolicy
import dev.failsafe.okhttp.FailsafeCall'''
            apiClientFileText = apiClientFileText.replaceFirst('import ', newImports + '\nimport ')

        }
        apiClientFile.write(apiClientFileText)


        updateDomainClientsWithFailsafe('build/generated/api/client2/src/main/kotlin/io/airbyte/api/client2/generated')
    }
}

def genApiDocs = tasks.register("generateApiDocs", GenerateTask) {
    def docsOutputDir = "$buildDir/generated/api/docs"

    generatorName = "html"
    inputSpec = specFile
    outputDir = docsOutputDir

    apiPackage = "io.airbyte.api.client.generated"
    invokerPackage = "io.airbyte.api.client.invoker.generated"
    modelPackage = "io.airbyte.api.client.model.generated"

    schemaMappings = [
            'OAuthConfiguration'                : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceDefinitionSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'               : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationDefinitionSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'StreamJsonSchema'                  : 'com.fasterxml.jackson.databind.JsonNode',
            'StateBlob'                         : 'com.fasterxml.jackson.databind.JsonNode',
            'FieldSchema'                       : 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            dateLibrary  : "java8",
            generatePom  : "false",
            interfaceOnly: "true"
    ]
}

def genAirbyteApiServer = tasks.register('generateAirbyteApiServer', GenerateTask) {
    def serverOutputDir = "$buildDir/generated/airbyte_api/server"

    inputs.file airbyteApiSpecFile
    outputs.dir serverOutputDir

    generatorName = "jaxrs-spec"
    inputSpec = airbyteApiSpecFile
    outputDir = serverOutputDir
    templateDir = airbyteApiSpecTemplateDirApi

    apiPackage = "io.airbyte.airbyte-api.generated"
    invokerPackage = "io.airbyte.airbyte-api.invoker.generated"
    modelPackage = "io.airbyte.airbyte-api.model.generated"

    generateApiDocumentation = false

    configOptions = [
            dateLibrary                   : "java8",
            generatePom                   : "false",
            interfaceOnly                 : "true",
            returnResponse                : "true",
            useBeanValidation             : "true",
            performBeanValidation         : "true",
            additionalModelTypeAnnotations: "@io.micronaut.core.annotation.Introspected",
            additionalEnumTypeAnnotations : "@io.micronaut.core.annotation.Introspected"
    ]

    schemaMappings = [
            'SourceConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'OAuthInputConfiguration'      : 'com.fasterxml.jackson.databind.JsonNode',
            'OAuthCredentialsConfiguration': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'     : 'com.fasterxml.jackson.databind.JsonNode',
    ]
}

// TODO: Linked to document okhhtp
def genWorkloadApiClient = tasks.register("genWorkloadApiClient", GenerateTask) {
    def clientOutputDir = "$buildDir/generated/workloadapi/client"

    inputs.file workloadSpecFile
    outputs.dir clientOutputDir

    generatorName = "kotlin"
    inputSpec = workloadSpecFile
    outputDir = clientOutputDir

    apiPackage = "io.airbyte.workload.api.client.generated"
    invokerPackage = "io.airbyte.workload.api.client.invoker.generated"
    modelPackage = "io.airbyte.workload.api.client.model.generated"

    schemaMappings = [
            'OAuthConfiguration'                : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceDefinitionSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'               : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationDefinitionSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration'          : 'com.fasterxml.jackson.databind.JsonNode',
            'StreamJsonSchema'                  : 'com.fasterxml.jackson.databind.JsonNode',
            'StateBlob'                         : 'com.fasterxml.jackson.databind.JsonNode',
            'FieldSchema'                       : 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            enumPropertyNaming  : "UPPERCASE",
            generatePom         : "false",
            interfaceOnly       : "true"
    ]

    doLast {
        // Delete file generated by the client2 task
        def dir = file('build/generated/workloadapi/client/src/main/kotlin/org').deleteDir()

        def generatedDomainClientsPath = 'build/generated/workloadapi/client/src/main/kotlin/io/airbyte/workload/api/client/generated'
        updateDomainClientsWithFailsafe(generatedDomainClientsPath)
        // the kotlin client (as opposed to the java client) doesn't include the response body in the exception message.
        updateDomainClientsToIncludeHttpResponseBodyOnClientException(generatedDomainClientsPath)
    }

    dependsOn(':airbyte-workload-api-server:compileKotlin', 'genApiClient2')
}


compileJava.dependsOn genApiDocs, genApiClient, genApiServer, genAirbyteApiServer

kapt {
    correctErrorTypes true
}

// uses afterEvaluate because at configuration time, the kaptGenerateStubsKotlin task does not exist.
afterEvaluate {
    tasks.named('kaptGenerateStubsKotlin').configure {
        mustRunAfter genApiDocs, genApiClient, genApiClient2, genApiServer, genAirbyteApiServer,
                genWorkloadApiClient
    }
}

tasks.named("compileKotlin") {
    dependsOn tasks.named("genApiClient2")
    dependsOn tasks.named("genWorkloadApiClient")
}

dependencies {
    annotationProcessor libs.micronaut.openapi
    kapt libs.micronaut.openapi

    compileOnly libs.v3.swagger.annotations
    kapt libs.v3.swagger.annotations

    implementation platform(libs.micronaut.bom)
    implementation libs.bundles.micronaut
    implementation libs.commons.io
    implementation libs.failsafe.okhttp
    implementation libs.guava
    implementation libs.javax.annotation.api
    implementation libs.javax.ws.rs.api
    implementation libs.javax.validation.api
    implementation libs.jackson.datatype
    implementation libs.moshi.kotlin
    implementation libs.okhttp
    implementation libs.openapi.jackson.databind.nullable
    implementation libs.reactor.core
    implementation libs.slf4j.api
    implementation libs.swagger.annotations

    testRuntimeOnly libs.junit.jupiter.engine
    testImplementation libs.bundles.junit
    testImplementation libs.assertj.core
    testImplementation libs.junit.pioneer
    testImplementation libs.mockk
    testImplementation libs.kotlin.test.runner.junit5
}

sourceSets {
    main {
        java {
            srcDirs "$buildDir/generated/api/server/src/gen/java",
                    "$buildDir/generated/airbyte_api/server/src/gen/java",
                    "$buildDir/generated/api/client/src/main/java",
                    "$buildDir/generated/api/client2/src/main/kotlin",
                    "$buildDir/generated/workloadapi/client/src/main/kotlin"
            "$projectDir/src/main/java"
        }
        resources {
            srcDir "$projectDir/src/main/openapi/"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = [
            '-parameters'
    ]
}

airbyte {
    spotless {
        excludes = ["src/main/openapi/workload-openapi.yaml"]
    }
}

private def updateDomainClientsWithFailsafe(def clientPath) {
    /*
     * UPDATE domain clients to use Failsafe.
     */
    def dir = file(clientPath)
    dir.eachFile { domainClient ->
        if (domainClient.name.endsWith('.kt')) {
            def domainClientFileText = domainClient.text

            // replace class declaration
            domainClientFileText = domainClientFileText.replaceAll(
                    /class (\S+)\(basePath: kotlin.String = defaultBasePath, client: OkHttpClient = ApiClient.defaultClient\) : ApiClient\(basePath, client\)/,
                    'class $1(basePath: kotlin.String = defaultBasePath, client: OkHttpClient = ApiClient.defaultClient, policy : RetryPolicy<okhttp3.Response> = RetryPolicy.ofDefaults()) : ApiClient(basePath, client, policy)'
            )

            // add imports if not exist
            if(!domainClientFileText.contains("import dev.failsafe.RetryPolicy")) {
                def newImports = "import dev.failsafe.RetryPolicy"
                domainClientFileText = domainClientFileText.replaceFirst('import ', newImports + '\nimport ')
            }

            domainClient.write(domainClientFileText)
        }
    }
}

private def updateDomainClientsToIncludeHttpResponseBodyOnClientException(def clientPath) {
    def dir = file(clientPath)
    dir.eachFile { domainClient ->
        if (domainClient.name.endsWith('.kt')) {
            def domainClientFileText = domainClient.text

            domainClientFileText = domainClientFileText.replace(
                    'throw ClientException("Client error : ${localVarError.statusCode} ${localVarError.message.orEmpty()}", localVarError.statusCode, localVarResponse)',
                    'throw ClientException("Client error : ${localVarError.statusCode} ${localVarError.message.orEmpty()} ${localVarError.body ?: ""}", localVarError.statusCode, localVarResponse)')

            domainClient.write(domainClientFileText)
        }
    }
}
