plugins {
    id 'java-library'
}

dependencies {
    implementation 'commons-cli:commons-cli:1.4'

    implementation project(':airbyte-commons')
    implementation project(':airbyte-commons-cli')
    implementation project(':airbyte-config:config-models')
    implementation libs.airbyte.protocol
    implementation project(':airbyte-json-validation')
}

task downloadConnectorRegistry(type: JavaExec, dependsOn: compileJava) {
    /**
     * run this once a day. if you want to force this task to run do so with --rerun
     * e.g. ./gradlew :airbyte-config:specs:downloadConnectorRegistry --info --rerun
     */
    inputs.property("todaysDate", new Date().clearTime() )
    outputs.upToDateWhen { true }
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.airbyte.config.specs.ConnectorRegistryDownloader'
    args project(":airbyte-config:init").projectDir
}

project(":airbyte-config:init").tasks.processResources.dependsOn(downloadConnectorRegistry)
project(":airbyte-config:init").tasks.processTestResources.dependsOn(downloadConnectorRegistry)
project(":airbyte-config:init").tasks.test.dependsOn(downloadConnectorRegistry)


task generateConnectorSpecsMask(type: JavaExec, dependsOn: downloadConnectorRegistry) {
    classpath = sourceSets.main.runtimeClasspath

    mainClass = 'io.airbyte.config.specs.ConnectorSpecMaskGenerator'

    args '--resource-root'
    args new File(project(":airbyte-config:init").projectDir, '/src/main/resources')
}

// TODO (ben): Remove once cloud is no longer depenedant on this.
task generateSeedConnectorSpecs(type: JavaExec, dependsOn: generateConnectorSpecsMask) {
    classpath = sourceSets.main.runtimeClasspath

    mainClass = 'io.airbyte.config.specs.SeedConnectorSpecGenerator'

    args '--seed-root'
    args new File(project(":airbyte-config:init").projectDir, '/src/main/resources/seed')
}

project(":airbyte-config:init").tasks.processResources.dependsOn(generateConnectorSpecsMask)

Task publishArtifactsTask = getPublishArtifactsTask("$rootProject.ext.version", project)
