plugins {
    id 'java-library'
}

dependencies {
    implementation 'commons-cli:commons-cli:1.4'

    implementation project(':airbyte-commons')
    implementation project(':airbyte-commons-cli')
    implementation project(':airbyte-config:config-models')
    implementation libs.airbyte.protocol
    implementation project(':airbyte-json-validation')
}

task downloadCombinedConnectorCatalog(type: JavaExec, dependsOn: compileJava) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.airbyte.config.specs.CombinedConnectorCatalogDownloader'
    args project(":airbyte-config:init").projectDir
}

project(":airbyte-config:init").tasks.processResources.dependsOn(downloadCombinedConnectorCatalog)
project(":airbyte-config:init").tasks.processTestResources.dependsOn(downloadCombinedConnectorCatalog)
project(":airbyte-config:init").tasks.test.dependsOn(downloadCombinedConnectorCatalog)


task generateConnectorSpecsMask(type: JavaExec, dependsOn: downloadCombinedConnectorCatalog) {
    classpath = sourceSets.main.runtimeClasspath

    mainClass = 'io.airbyte.config.specs.ConnectorSpecMaskGenerator'

    args '--resource-root'
    args new File(project(":airbyte-config:init").projectDir, '/src/main/resources')
}

project(":airbyte-config:init").tasks.processResources.dependsOn(generateConnectorSpecsMask)

Task publishArtifactsTask = getPublishArtifactsTask("$rootProject.ext.version", project)
