import groovy.json.JsonBuilder
import groovy.yaml.YamlSlurper

plugins {
    id 'application'
    id 'airbyte-integration-test-java'
}

configurations {
    jdbc
    airbyteProtocol
}

configurations.all {
    // The quartz-scheduler brings in a really old version of hikari, we do not want to inherit this version.
    exclude group: "com.zaxxer", module: "HikariCP-java7"
    resolutionStrategy {
        // Ensure that the versions defined in deps.toml are used
        // instead of versions from transitive dependencies
        force libs.flyway.core, libs.jooq, libs.s3, libs.aws.java.sdk.s3
    }
}

dependencies {
    annotationProcessor platform(libs.micronaut.bom)
    annotationProcessor libs.bundles.micronaut.annotation.processor

    implementation platform(libs.micronaut.bom)
    implementation libs.bundles.micronaut
    implementation libs.micronaut.cache.caffeine
    implementation libs.flyway.core
    implementation libs.jooq
    implementation libs.s3
    implementation libs.aws.java.sdk.s3
    implementation 'com.google.auth:google-auth-library-oauth2-http:1.4.0'
    implementation 'com.auth0:java-jwt:3.19.2'
    implementation libs.kubernetes.client
    implementation libs.guava
    implementation(libs.temporal.sdk) {
        exclude module: 'guava'
    }
    implementation libs.apache.ant
    implementation libs.apache.commons.lang
    implementation libs.apache.commons.text
    implementation libs.quartz.scheduler
    implementation libs.micrometer.statsd
    implementation libs.bundles.datadog
    implementation libs.sentry.java
    implementation libs.byte.buddy
    implementation libs.spring.core

    implementation project(':airbyte-analytics')
    implementation project(':airbyte-api')
    implementation project(':airbyte-commons')
    implementation project(':airbyte-commons-converters')
    implementation project(':airbyte-commons-micronaut')
    implementation project(':airbyte-commons-protocol')
    implementation project(':airbyte-commons-temporal')
    implementation project(':airbyte-commons-worker')
    implementation project(':airbyte-commons-server')
    implementation project(':airbyte-config:config-models')
    implementation project(':airbyte-config:config-persistence')
    implementation project(':airbyte-config:specs')
    implementation project(':airbyte-config:init')
    implementation project(':airbyte-db:jooq')
    implementation project(':airbyte-db:db-lib')
    implementation project(':airbyte-featureflag')
    implementation project(':airbyte-metrics:metrics-lib')
    implementation project(':airbyte-json-validation')
    implementation libs.airbyte.protocol
    implementation project(':airbyte-notification')
    implementation(project(':airbyte-persistence:job-persistence')) {
        // Temporary hack to avoid dependency conflicts
        exclude group: 'io.micronaut'
        exclude group: 'io.micronaut.flyway'
        exclude group: 'io.micronaut.jaxrs'
        exclude group: 'io.micronaut.security'
        exclude group: 'io.micronaut.sql'
    }
    implementation project(':airbyte-worker-models')

    runtimeOnly libs.javax.databind

    testAnnotationProcessor platform(libs.micronaut.bom)
    testAnnotationProcessor libs.bundles.micronaut.test.annotation.processor

    integrationTestJavaAnnotationProcessor platform(libs.micronaut.bom)
    integrationTestJavaAnnotationProcessor libs.bundles.micronaut.test.annotation.processor

    testImplementation libs.bundles.micronaut.test
    testImplementation libs.temporal.testing
    testImplementation libs.json.path
    testImplementation libs.mockito.inline
    testImplementation libs.postgresql
    testImplementation libs.platform.testcontainers
    testImplementation libs.platform.testcontainers.postgresql
    testImplementation project(':airbyte-test-utils')
    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.66'
    testImplementation 'org.bouncycastle:bcpkix-jdk15on:1.66'
    testImplementation project(':airbyte-api')

    integrationTestJavaImplementation project(':airbyte-workers')
    integrationTestJavaImplementation libs.bundles.micronaut.test

    airbyteProtocol(libs.airbyte.protocol) {
        transitive = false
    }
}

application {
    applicationName = project.name
    mainClass = 'io.airbyte.workers.Application'
    applicationDefaultJvmArgs = ['-XX:+ExitOnOutOfMemoryError', '-XX:MaxRAMPercentage=75.0']
}

Properties env = new Properties()
rootProject.file('.env.dev').withInputStream { env.load(it) }

run {
    // default for running on local machine.
    env.each { entry ->
        environment entry.getKey(), entry.getValue()
    }

    environment 'AIRBYTE_ROLE', System.getenv('AIRBYTE_ROLE')
    environment 'AIRBYTE_VERSION', env.VERSION
    environment 'MICRONAUT_ENVIRONMENTS', 'control-plane'
}

task cloudStorageIntegrationTest(type: Test) {
    useJUnitPlatform {
        includeTags cloudStorageTestTagName
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Duplicated in :airbyte-container-orchestrator, eventually, this should be handled in :airbyte-protocol
task generateWellKnownTypes() {
    doLast {
        def wellKnownTypesYamlPath = 'airbyte_protocol/well_known_types.yaml'
        configurations.airbyteProtocol.getFiles().each {
            def zip = new java.util.zip.ZipFile(it)
            def entry = zip.getEntry(wellKnownTypesYamlPath)

            def wellKnownTypesYaml = zip.getInputStream(entry).text
            def parsedYaml = new YamlSlurper().parseText(wellKnownTypesYaml)
            def wellKnownTypesJson = new JsonBuilder(parsedYaml).toPrettyString()
            def targetFile = project.file("build/docker/WellKnownTypes.json")
            targetFile.getParentFile().mkdirs()
            targetFile.text = wellKnownTypesJson
        }
    }
}

tasks.named("buildDockerImage") {
    dependsOn copyGeneratedTar
    dependsOn generateWellKnownTypes
}

// Ideally, we would have buildDockerImage depend on generateWellKnownTypes
// but some of our actions use copyGeneratedTar as the "set up the docker build context" task
// so we'll just add it here.
tasks.named("copyGeneratedTar") {
    dependsOn generateWellKnownTypes
}

Task publishArtifactsTask = getPublishArtifactsTask("$rootProject.ext.version", project)
